;--------------------------------
;Includes
  SetCompressor /SOLID lzma

  !define MULTIUSER_EXECUTIONLEVEL Highest
  !define MULTIUSER_MUI
  !include MultiUser.nsh
  !include MUI2.nsh

;--------------------------------
;General

  !define APPNAME "Tcl 8.6 for Windows (Unofficial)"
  !define VERSION "8.6.6-0.3.0"
  
  ;Name and file
  Name "${APPNAME}"
  OutFile "tcl86-installer-${VERSION}.exe"

  ;Default installation folder
  ;InstallDir

  ;Get installation folder from registry if available
  ;InstallDirRegKey SHCTX "Software\${APPNAME}" ""

  ;Request application privileges for Windows Vista
  RequestExecutionLevel user

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING

;--------------------------------
;Pages

  !insertmacro MULTIUSER_PAGE_INSTALLMODE
  !insertmacro MUI_PAGE_COMPONENTS
  !define MUI_PAGE_CUSTOMFUNCTION_PRE DirectoryPre
  !insertmacro MUI_PAGE_DIRECTORY
  !insertmacro MUI_PAGE_INSTFILES
  
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  
;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"

;--------------------------------
;Installer Sections

Section "Tcl 8.6 for Windows"

  SetOutPath "$INSTDIR"
  
  ;Begin create list
CREATE_LIST
  ;End create list
  
  ;Store installation folder
  WriteRegStr SHCTX "Software\${APPNAME}" "" $INSTDIR
  
  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" \
              "DisplayName" "${APPNAME}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" \
              "DisplayVersion" "${VERSION}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}" \
              "UninstallString" "$INSTDIR\Uninstall.exe"
SectionEnd

Section "Start menu shortcuts"
  createDirectory "$SMPROGRAMS\${APPNAME}"
  createShortCut "$SMPROGRAMS\${APPNAME}\Tcl command shell.lnk" "$INSTDIR\bin\tclsh.exe" "" "" "" "" "" "Console for interactive execution of commands in the Tcl language"
  createShortCut "$SMPROGRAMS\${APPNAME}\Tk graphical console.lnk" "$INSTDIR\bin\wish.exe" "" "" "" "" "" "Graphical console for interactive execution of commands using the Tcl/Tk toolkit"
  createShortCut "$SMPROGRAMS\${APPNAME}\Tkcon enhanced console.lnk" "$INSTDIR\bin\wish.exe" "$\"$INSTDIR\bin\tkcon.tcl$\"" "" "" "" "" "Graphical enhanced console for interactive execution of commands using the Tcl/Tk toolkit"
  createShortCut "$SMPROGRAMS\${APPNAME}\Uninstall.lnk" "$INSTDIR\Uninstall.exe" "" "" "" "" "" "Uninstall ${APPNAME}"
SectionEnd

;--------------------------------
;Uninstaller Section

Section "Uninstall"

  ;Begin delete list
DELETE_LIST
  ;End delete list

  Delete "$SMPROGRAMS\${APPNAME}\Tcl command shell.lnk"
  Delete "$SMPROGRAMS\${APPNAME}\Tk graphical console.lnk"
  Delete "$SMPROGRAMS\${APPNAME}\Tkcon enhanced console.lnk"
  Delete "$SMPROGRAMS\${APPNAME}\Uninstall.lnk"
  RMDir "$SMPROGRAMS\${APPNAME}"

  Delete "$INSTDIR\Uninstall.exe"

  RMDir "$INSTDIR"

  DeleteRegKey /ifempty SHCTX "Software\${APPNAME}"
  DeleteRegKey SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPNAME}"
SectionEnd

Function .onInit
  !insertmacro MULTIUSER_INIT
FunctionEnd

Function un.onInit
  !insertmacro MULTIUSER_UNINIT
FunctionEnd

Function DirectoryPre
  ${If} $MultiUser.InstallMode == "AllUsers"
    StrCpy $INSTDIR "C:\Tcl86"
  ${Else}
    StrCpy $INSTDIR "$LOCALAPPDATA\Tcl86"
  ${EndIf}
FunctionEnd